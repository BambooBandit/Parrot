buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
    }
}

apply plugin: 'java-library'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'
apply from: 'publish.gradle'

def projectName = 'parrot'
version '0.0.1-SNAPSHOT'
group 'com.rafaskoberg.gdx'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            // The -quiet is because of some sort of weird JDK JavaCompiler bug:
            // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
            options.addStringOption('Xdoclint:all,-missing', '-quiet')
        }
    }
}

allprojects {
    apply plugin: 'java-library'

    sourceCompatibility = 1.8
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    sourceSets.main.java.srcDirs = ["src/main/java"]
    sourceSets.test.java.srcDirs = ["src/test/java"]

    ext {
        gdxVersion = '1.9.10'
        boomVersion = '561816962ed2f842bba5e6468861d4cc18b7eb86'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url 'https://jitpack.io' }
    }
}

project(":core") {
    dependencies {
        api group: 'com.badlogicgames.gdx', name: 'gdx', version: gdxVersion
        compile "com.github.rafaskb.boom:core:$boomVersion"
    }
}

project(":lwjgl3") {
    dependencies {
        api project(":core")
        compile "com.github.rafaskb.boom:lwjgl3:$boomVersion"

        testImplementation group: 'com.badlogicgames.gdx', name: 'gdx-backend-lwjgl3', version: gdxVersion
        testImplementation group: 'com.badlogicgames.gdx', name: 'gdx-platform', version: gdxVersion, classifier: 'natives-desktop'
    }
}